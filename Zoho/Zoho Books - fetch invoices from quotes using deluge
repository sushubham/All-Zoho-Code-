
sync_id = cm_sync.get("module_record_id");
org_id = organization.get("organization_id");
syncResp = invokeurl
[
	url :"https://www.zohoapis.com/books/v3/cm_sync/" + sync_id + "?organization_id=" + org_id
	type :GET
	connection:"zohobooks_connection"
];
syncRec = syncResp.get("module_record");
quote_number = syncRec.get("cf_quote_number");
quoteSearchResp = invokeurl
[
	url :"https://www.zohoapis.com/books/v3/estimates?organization_id=" + org_id + "&estimate_number=" + quote_number
	type :GET
	connection:"zohobooks_connection"
];
quotes = quoteSearchResp.get("estimates");
if(quotes.size() == 0)
{
	return {"error":"Quote not found with number: " + quote_number};
}
quote_id = quotes.get(0).get("estimate_id");
quoteResp = invokeurl
[
	url :"https://www.zohoapis.com/books/v3/estimates/" + quote_id + "?organization_id=" + org_id
	type :GET
	connection:"zohobooks_connection"
];
quote = quoteResp.get("estimate");
newLineItems = List();
for each  li in quote.get("line_items")
{
	lineMap = Map();
	lineMap.put("item_id",li.get("item_id"));
	lineMap.put("rate",li.get("rate"));
	lineMap.put("quantity",li.get("quantity"));
	if(li.containsKey("description") && li.get("description") != null && li.get("description") != "")
	{
		lineMap.put("description",li.get("description"));
	}
	if(li.containsKey("tax_id") && li.get("tax_id") != null && li.get("tax_id") != "")
	{
		lineMap.put("tax_id",li.get("tax_id"));
	}
	isValidDiscount = false;
	discountValue = li.get("discount");
	if(discountValue != null)
	{
		try 
		{
			discountDecimal = discountValue.toDecimal();
			if(discountDecimal > 0)
			{
				isValidDiscount = true;
			}
		}
		catch (e)
		{
			isValidDiscount = false;
		}
	}
	if(isValidDiscount)
	{
		lineMap.put("discount",discountValue);
	}
	newLineItems.add(lineMap);
}
invoiceMap = Map();
invoiceMap.put("customer_id",quote.get("customer_id"));
invoiceMap.put("reference_number",quote.get("estimate_number"));
invoiceMap.put("line_items",newLineItems);
invoiceResp = invokeurl
[
	url :"https://www.zohoapis.com/books/v3/invoices?organization_id=" + org_id
	type :POST
	parameters:invoiceMap.toString()
	connection:"zohobooks_connection"
];
if(!invoiceResp.containsKey("invoice"))
{
	return {"error":"Invoice creation failed","response":invoiceResp.toString()};
}
invoice = invoiceResp.get("invoice");
invoice_number = invoice.get("invoice_number");
updateMap = Map();
updateMap.put("invoice_number",invoice_number);
updateResp = invokeurl
[
	url :"https://www.zohoapis.com/books/v3/cm_sync/" + sync_id + "?organization_id=" + org_id
	type :PUT
	parameters:updateMap.toString()
	connection:"zohobooks_connection"
];
responseMap = Map();
responseMap.put("message","Invoice Created Successfully: " + invoice_number);
responseMap.put("invoice_number",invoice_number);
return responseMap;
